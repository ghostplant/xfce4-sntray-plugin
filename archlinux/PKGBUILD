# Maintainer: rilian-la-te <ria.freelander@gmail.com>

pkgname=xfce4-sntray-plugin
_cmakename=cmake-vala
pkgver=0.3.0
pkgrel=1
pkgdesc="Plugin for xfce4-panel to show StatusNotifierItems (AppIndicators) via FlowBox"
url="https://github.com/rilian-la-te/xfce4-sntray-plugin"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gtk3' 'xfce4-panel>=4.11.2')
makedepends=('cmake' 'vala')
source=("https://github.com/rilian-la-te/xfce4-sntray-plugin/archive/${pkgver}.tar.gz"
        "git://github.com/rilian-la-te/${_cmakename}.git")
sha256sums=('fe9ee06ed3b7559ab9bf140b4e1ae2cbe9644336f4ed961a099ca13138b3d918'
            'SKIP')

prepare() {
  cd "${srcdir}/${_cmakename}"
  cp -r . "${srcdir}/${pkgname}-${pkgver}/cmake/vala"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}



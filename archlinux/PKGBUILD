# Maintainer: rilian-la-te <ria.freelander@gmail.com>

pkgname=xfce4-sntray-plugin
_cmakename=cmake-vala
pkgver=0.3.1
pkgrel=2
pkgdesc="Plugin for xfce4-panel to show StatusNotifierItems (AppIndicators) via FlowBox"
url="https://github.com/rilian-la-te/xfce4-sntray-plugin"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gtk3' 'xfce4-panel>=4.11.2')
makedepends=('cmake' 'vala')
optdepends=('vala-panel-extras-volume: ALSA volume applet'
            'vala-panel-extras-battery: UPower battery applet'
            'vala-panel-extras-weather: Weather applet'
            'vala-panel-extras-xkb: XKB applet')
source=("https://github.com/rilian-la-te/xfce4-sntray-plugin/archive/${pkgver}.tar.gz"
        "git://github.com/rilian-la-te/${_cmakename}.git")
sha256sums=('b2764bb6de93a585772469f34a17eff44ab7635fbb8b5004cd09b5ad21651d53'
            'SKIP')

prepare() {
  cd "${srcdir}/${_cmakename}"
  cp -r . "${srcdir}/${pkgname}-${pkgver}/cmake/vala"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}



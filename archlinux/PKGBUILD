# Maintainer: rilian-la-te <ria.freelander@gmail.com>

pkgname=xfce4-sntray-plugin
_cmakename=cmake-vala
pkgver=0.3.3
pkgrel=1
pkgdesc="Plugin for xfce4-panel to show StatusNotifierItems (AppIndicators) via FlowBox"
url="https://github.com/rilian-la-te/xfce4-sntray-plugin"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gtk3' 'xfce4-panel>=4.11.2')
makedepends=('cmake' 'vala')
optdepends=('vala-panel-extras-volume: ALSA volume applet'
            'vala-panel-extras-battery: UPower battery applet'
            'vala-panel-extras-weather: Weather applet'
            'vala-panel-extras-xkb: XKB applet'
            'sni-qt: Qt applications for StatusNotifier'
            'appindicator-gtk2: Gtk2 applications for StatusNotifier'
            'appindicator-gtk3: Gtk3 applications for StatusNotifier')
source=("https://github.com/rilian-la-te/xfce4-sntray-plugin/archive/${pkgver}.tar.gz"
        "git://github.com/rilian-la-te/${_cmakename}.git")
sha256sums=('d359ae97caa9bc17d240ec8bcc280f9110423387ad1690fca0776e0b72c79f82'
            'SKIP')

prepare() {
  cd "${srcdir}/${_cmakename}"
  cp -r . "${srcdir}/${pkgname}-${pkgver}/cmake/vala"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}


